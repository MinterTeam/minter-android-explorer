/*
 * Copyright (C) by MinterTeam. 2019
 * @link <a href="https://github.com/MinterTeam">Org Github</a>
 * @link <a href="https://github.com/edwardstock">Maintainer Github</a>
 *
 * The MIT License
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 */
buildscript {
    repositories {
        google()
        jcenter()
        mavenLocal()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.2'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }
}

allprojects {
    repositories {
        mavenLocal()
        google()
        jcenter()
        maven { url "https://dl.bintray.com/ethereum/maven/" }
        maven { url 'https://clojars.org/repo/' }
        maven { url 'https://jitpack.io' }
        maven { url 'https://dl.bintray.com/minterteam/android' }
        maven { url "https://repo1.maven.org/maven2/com/google/zxing/" }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/'}
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
//apply plugin: 'jacoco'

group = 'network.minter.android'
version = '0.6.1'

ext {
    minterMinSdk = 16
    minterMaxSdk = 29
    minterBuildTools = "29.0.2"
    minterCoreSDK = "0.6.2"
    minterBlockchainSDK = "0.10.0"

    buildArtifactName = project.name
    buildArtifactVersion = version
    buildArtifactGroup = group

    pomName = "Minter Explorer"
    pomUrl = "https://github.com/MinterTeam/minter-android-explorer"
    pomScm = {
        connection = "scm:git:git://github.com/MinterTeam/minter-android-explorer.git"
        url = pomUrl
    }
    pomInceptionYear = "2018"
    pomContributors = {}
    pomDescription = "Minter Android Explorer API SDK"
    pomDevelopers = {
        developer {
            id = "edwardstock"
            name = "Eduard Maximovich"
            email = "edwatd.vstock@gmail.com"
            roles = ["maintainer"]
            timezone = "Europe/Moscow"
        }
    }
    pomLicenses = {
        license {
            name = "MIT License"
            url = "https://opensource.org/licenses/MIT"
            distribution = "repo"
        }
    }

}

android {
    compileSdkVersion minterMaxSdk
    buildToolsVersion  minterBuildTools

    defaultConfig {
        minSdkVersion minterMinSdk
        targetSdkVersion minterMaxSdk
        versionCode 2
        versionName version

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled true
            proguardFiles  getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debugTest {
            minifyEnabled false
            testCoverageEnabled true
            proguardFiles  getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "env"
    productFlavors {
        // can't use name starts with 'test'
        netMain {
            dimension "env"
            buildConfigField "String", "BASE_API_URL", '"https://explorer-api.apps.minter.network/api/"'
            buildConfigField "String", "BASE_FRONT_URL", '"https://explorer.minter.network"'
            buildConfigField "String", "GATE_API_URL", '"https://gate.apps.minter.network/api/"'
        }
        netTest {
            dimension "env"
            buildConfigField "String", "BASE_API_URL", '"https://explorer-api.testnet.minter.network/api/"'
            buildConfigField "String", "BASE_FRONT_URL", '"https://testnet.explorer.minter.network"'
            buildConfigField "String", "GATE_API_URL", '"https://gate.minter.network/api/"'
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    netTestImplementation "network.minter.android:minter-android-core-testnet:${minterCoreSDK}"
    netMainImplementation "network.minter.android:minter-android-core:${minterCoreSDK}"
    netTestImplementation "network.minter.android:minter-android-blockchain-testnet:${minterBlockchainSDK}"
    netMainImplementation "network.minter.android:minter-android-blockchain:${minterBlockchainSDK}"

    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'

    // network
    implementation 'com.squareup.okhttp3:okhttp:3.12.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.0'
    implementation 'com.squareup.retrofit2:retrofit:2.6.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.0'

    // stuff
    implementation 'org.parceler:parceler-api:1.1.12'
    annotationProcessor 'org.parceler:parceler:1.1.12'
    implementation 'com.annimon:stream:1.2.1'

    testImplementation 'junit:junit:4.12'
    testImplementation 'com.squareup.okhttp3:mockwebserver:3.12.1'
    testImplementation 'com.squareup.retrofit2:retrofit-mock:2.5.0'
    testImplementation "org.mockito:mockito-core:2.23.4"
    testImplementation "org.mockito:mockito-android:2.23.4"
    testImplementation 'org.mockito:mockito-inline:2.23.4'
}

apply from: 'scripts/minter_publish.gradle'
//apply from: 'scripts/jacoco.gradle'
